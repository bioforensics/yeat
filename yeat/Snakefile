# -------------------------------------------------------------------------------------------------
# Copyright (c) 2021, DHS. This file is part of YEAT: http://github.com/bioforensics/yeat
#
# This software was prepared for the Department of Homeland Security (DHS) by the Battelle National
# Biodefense Institute, LLC (BNBI) as part of contract HSHQDC-15-C-00064 to manage and operate the
# National Biodefense Analysis and Countermeasures Center (NBACC), a Federally Funded Research and
# Development Center.
# -------------------------------------------------------------------------------------------------

import json
from shutil import copyfile
import pandas as pd
from pathlib import Path
from random import randint


rule all:
    input:
        expand("analysis/quast/{assembler}/{sample}_report.html", assembler=config["assemblers"], sample=config["sample"]),
        expand("seq/fastqc/{sample}_{reads}_fastqc.html", sample=config["sample"], reads=["R1", "R2"])


rule copyinput:
    output:
        read1="seq/input/{sample}_R1.fq.gz",
        read2="seq/input/{sample}_R2.fq.gz"
    input:
        read1=config["read1"],
        read2=config["read2"]
    run:
        if input.read1[-3:] == ".gz" and input.read2[-3:] == ".gz":
            copyfile(input.read1, output.read1)
            copyfile(input.read2, output.read2)
        elif input.read1[-3:] != ".gz" and input.read2[-3:] == ".gz":
            shell("gzip {input.read1} > {output.read1}")
            copyfile(input.read2, output.read2)
        elif input.read1[-3:] == ".gz" and input.read2[-3:] != ".gz":
            copyfile(input.read1, output.read1)
            shell("gzip {input.read2} > {output.read2}")
        else:
            copyfile(input.read1, params.file1)
            copyfile(input.read2, params.file2)
            shell("gzip seq/input/*")


rule fastqc:
    output:
        out1="seq/fastqc/{sample}_R1_fastqc.html",
        out2="seq/fastqc/{sample}_R2_fastqc.html"
    input:
        read1="seq/input/{sample}_R1.fq.gz",
        read2="seq/input/{sample}_R2.fq.gz"
    threads: 128
    shell:
        """
        fastqc {input.read1} {input.read2} --threads {threads} -o seq/fastqc/
        """


rule fastp:
    output:
        out1="seq/fastp/{sample}.R1.fq.gz",
        out2="seq/fastp/{sample}.R2.fq.gz"
    input:
        read1="seq/input/{sample}_R1.fq.gz",
        read2="seq/input/{sample}_R2.fq.gz"
    shell:
        """
        fastp -i {input.read1} -I {input.read2} -o {output.out1} -O {output.out2} \
            --html seq/fastp/fastp.html --json seq/fastp/fastp.json \
            2> seq/fastp/report.txt
        """


rule mash:
    output:
        sketch="seq/mash/{sample}.R1.fq.gz.msh",
        mash_report="seq/mash/{sample}.report.tsv"
    input:
        read1="seq/fastp/{sample}.R1.fq.gz"
    shell:
        """
        mash sketch {input.read1} -o {output.sketch}
        mash info -t {output.sketch} > {output.mash_report}
        """


rule downsample:
    output:
        sub1="seq/downsample/{sample}.R1.fq.gz",
        sub2="seq/downsample/{sample}.R2.fq.gz"
    input:
        read1="seq/fastp/{sample}.R1.fq.gz",
        read2="seq/fastp/{sample}.R2.fq.gz",
        mash_report="seq/mash/{sample}.report.tsv"
    params:
        coverage=config["coverage"],
        downsample=config["downsample"],
        fastp_report="seq/fastp/fastp.json",
        genome_size=config["genomesize"],
        seed=config["seed"]
    run:
        if params.downsample == -1:
            p = Path("seq/downsample")
            p.mkdir(parents=True, exist_ok=True)
            copyfile(input.read1, output.sub1)
            copyfile(input.read2, output.sub2)
            return
        if params.genome_size == 0:
            df = pd.read_csv(input.mash_report, sep="\t")
            genome_size = df["Length"].iloc[0]
        else:
            genome_size = params.genome_size
        with open(params.fastp_report, "r") as fh:
            qcdata = json.load(fh)
        base_count = qcdata["summary"]["after_filtering"]["total_bases"]
        read_count = qcdata["summary"]["after_filtering"]["total_reads"]
        avg_read_length = base_count / read_count
        if params.downsample == 0:
            down = int((genome_size * params.coverage) / (2 * avg_read_length))
        else:
            down = params.downsample
        if params.seed == None:
            seed = randint(1, 2**16-1)
        else:
            seed = params.seed
        print(f"[yeat] genome size: {genome_size}")
        print(f"[yeat] average read length: {avg_read_length}")
        print(f"[yeat] target depth of coverage: {params.coverage}x")
        print(f"[yeat] number of reads to sample: {down}")
        print(f"[yeat] random seed for sampling: {seed}")
        shell("seqtk sample -s {seed} {input.read1} {down} > seq/downsample/{wildcards.sample}.R1.fq")
        shell("seqtk sample -s {seed} {input.read2} {down} > seq/downsample/{wildcards.sample}.R2.fq")
        shell("gzip -f seq/downsample/*")


rule spades:
    output:
        contigs="analysis/spades/{sample}_contigs.fasta"
    input:
        read1="seq/downsample/{sample}.R1.fq.gz",
        read2="seq/downsample/{sample}.R2.fq.gz"
    threads: 128
    params:
        extra_args=config["extra_args"]
    shell:
        """
        spades.py -1 {input.read1} -2 {input.read2} -t {threads} -o analysis/spades {params.extra_args[spades]}
        ln -s scaffolds.fasta {output.contigs}
        """


rule megahit:
    output:
        contigs="analysis/megahit/{sample}_contigs.fasta"
    input:
        read1="seq/downsample/{sample}.R1.fq.gz",
        read2="seq/downsample/{sample}.R2.fq.gz"
    threads: 128
    params:
        extra_args=config["extra_args"]
    shell:
        """
        megahit -1 {input.read1} -2 {input.read2} -t {threads} -o analysis/megahit-temp {params.extra_args[megahit]}
        mv analysis/megahit-temp/* analysis/megahit
        rm -r analysis/megahit-temp
        ln -s final.contigs.fa {output.contigs}
        """


rule unicycler:
    output:
        contigs="analysis/unicycler/{sample}_contigs.fasta"
    input:
        read1="seq/downsample/{sample}.R1.fq.gz",
        read2="seq/downsample/{sample}.R2.fq.gz"
    params:
        extra_args=config["extra_args"]
    shell:
        """
        unicycler -1 {input.read1} -2 {input.read2} -t {threads} -o analysis/unicycler {params.extra_args[unicycler]}
        ln -s assembly.fasta {output.contigs}
        """


rule quast:
    output:
        report="analysis/quast/{assembler}/{sample}_report.html"
    input:
        contigs="analysis/{assembler}/{sample}_contigs.fasta"
    shell:
        """
        quast.py {input.contigs} -o analysis/quast/{wildcards.assembler}
        ln -s report.html {output.report}
        """

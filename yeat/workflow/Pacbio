# -------------------------------------------------------------------------------------------------
# Copyright (c) 2023, DHS. This file is part of YEAT: http://github.com/bioforensics/yeat
#
# This software was prepared for the Department of Homeland Security (DHS) by the Battelle National
# Biodefense Institute, LLC (BNBI) as part of contract HSHQDC-15-C-00064 to manage and operate the
# National Biodefense Analysis and Countermeasures Center (NBACC), a Federally Funded Research and
# Development Center.
# -------------------------------------------------------------------------------------------------

from yeat.workflow.aux import get_canu_readtype_flag


rule flye:
    output:
        contigs="analysis/{sample}/{readtype,pacbio-raw|pacbio-corr|pacbio-hifi}/{label}/flye/contigs.fasta"
    input:
        reads="seq/input/{sample}/{readtype}/combined-reads.fq.gz"
    threads: 128
    params:
        outdir="analysis/{sample}/{readtype}/{label}/flye",
        extra_args=lambda wildcards: config["assemblies"][wildcards.label].extra_args
    shell:
        """
        flye --{wildcards.readtype} {input.reads} -t {threads} -o {params.outdir} {params.extra_args}
        ln -s assembly.fasta {output.contigs}
        """


rule canu:
    output:
        contigs="analysis/{sample}/{readtype,pacbio-raw|pacbio-corr|pacbio-hifi}/{label}/canu/contigs.fasta"
    input:
        reads="seq/input/{sample}/{readtype}/combined-reads.fq.gz"
    threads: 128
    params:
        readtype_flag=lambda wildcards: get_canu_readtype_flag(wildcards.readtype),
        outdir="analysis/{sample}/{readtype}/{label}/canu",
        extra_args=lambda wildcards: config["assemblies"][wildcards.label].extra_args,
        grid=config["grid"]
    shell:
        """
        canu {params.readtype_flag} {input.reads} maxThreads={threads} -p {wildcards.sample} -d {params.outdir} {params.extra_args} useGrid=false
        ln -s {wildcards.sample}.contigs.fasta {output.contigs}
        """


rule hifiasm:
    output:
        contigs="analysis/{sample}/{readtype,pacbio-raw|pacbio-corr|pacbio-hifi}/{label}/hifiasm/contigs.fasta"
    input:
        reads="seq/input/{sample}/{readtype}/combined-reads.fq.gz"
    threads: 128
    params:
        prefix="analysis/{sample}/{readtype}/{label}/hifiasm/{sample}",
        extra_args=lambda wildcards: config["assemblies"][wildcards.label].extra_args
    shell:
        """
        hifiasm -o {params.prefix} -t {threads} {params.extra_args} {input.reads}
        gfatools gfa2fa {params.prefix}.bp.p_ctg.gfa > {params.prefix}.bp.p_ctg.fa
        ln -s {wildcards.sample}.bp.p_ctg.fa {output.contigs}
        """


rule hifiasm_meta:
    output:
        contigs="analysis/{sample}/{readtype,pacbio-raw|pacbio-corr|pacbio-hifi}/{label}/hifiasm_meta/contigs.fasta"
    input:
        reads="seq/input/{sample}/{readtype}/combined-reads.fq.gz"
    threads: 128
    params:
        prefix="analysis/{sample}/{readtype}/{label}/hifiasm_meta/{sample}",
        extra_args=lambda wildcards: config["assemblies"][wildcards.label].extra_args
    shell:
        """
        hifiasm_meta -o {params.prefix} -t {threads} {params.extra_args} {input.reads}
        gfatools gfa2fa {params.prefix}.p_ctg.gfa > {params.prefix}.p_ctg.fa
        ln -s {wildcards.sample}.p_ctg.fa {output.contigs}
        """


rule unicycler:
    output:
        contigs="analysis/{sample}/{readtype,pacbio-raw|pacbio-corr|pacbio-hifi}/{label}/unicycler/contigs.fasta"
    input:
        reads="seq/input/{sample}/{readtype}/combined-reads.fq.gz"
    threads: 128
    params:
        outdir="analysis/{sample}/{readtype}/{label}/unicycler",
        extra_args=lambda wildcards: config["assemblies"][wildcards.label].extra_args
    shell:
        """
        unicycler -l {input.reads} -t {threads} -o {params.outdir} {params.extra_args}
        ln -s assembly.fasta {output.contigs}
        """


rule metamdbg:
    output:
        contigs="analysis/{sample}/{readtype,pacbio-raw|pacbio-corr|pacbio-hifi}/{label}/metamdbg/contigs.fasta"
    input:
        reads="seq/input/{sample}/{readtype}/combined-reads.fq.gz"
    conda:
        "metaMDBG"
    threads: 128
    params:
        outdir="analysis/{sample}/{readtype}/{label}/metamdbg",
        extra_args=lambda wildcards: config["assemblies"][wildcards.label].extra_args
    shell:
        """
        metaMDBG asm {params.outdir} {input.reads} -t {threads} {params.extra_args}
        gunzip {params.outdir}/contigs.fasta.gz
        """

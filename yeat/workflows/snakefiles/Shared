# -------------------------------------------------------------------------------------------------
# Copyright (c) 2023, DHS. This file is part of YEAT: http://github.com/bioforensics/yeat
#
# This software was prepared for the Department of Homeland Security (DHS) by the Battelle National
# Biodefense Institute, LLC (BNBI) as part of contract HSHQDC-15-C-00064 to manage and operate the
# National Biodefense Analysis and Countermeasures Center (NBACC), a Federally Funded Research and
# Development Center.
# -------------------------------------------------------------------------------------------------

from shutil import copyfile


rule copyinput:
    output:
        combined="seq/input/{sample}/{readtype}/combined-reads.fq.gz"
    input:
        reads=lambda wildcards: config["samples"][wildcards.sample].data[wildcards.readtype]
    params:
        outdir="seq/input/{sample}/{readtype}"
    run:
        for i, inread in enumerate(input):
            outread = f"seq/input/{wildcards.sample}/{wildcards.readtype}/reads{i}.fq"
            if inread.endswith(".gz"):
                shell("gunzip -c {inread} > {outread}")
            else:
                copyfile(inread, outread)
        shell("cat {params.outdir}/*.fq > {params.outdir}/combined-reads.fq")
        shell("gzip {params.outdir}/combined-reads.fq")


rule fastqc:
    output:
        report="seq/fastqc/{sample}/{readtype}/combined-reads_fastqc.html"
    input:
        reads="seq/input/{sample}/{readtype}/combined-reads.fq.gz"
    threads: 128
    params:
        outdir="seq/fastqc/{sample}/{readtype}"
    shell:
        """
        fastqc {input.reads} -t {threads} -o {params.outdir}
        """


rule quast:
    output:
        report="analysis/{sample}/{readtype}/{label}/{assembler}/quast/report.html"
    input:
        contigs="analysis/{sample}/{readtype}/{label}/{assembler}/contigs.fasta"
    params:
        outdir="analysis/{sample}/{readtype}/{label}/{assembler}/quast"
    shell:
        """
        quast.py {input.contigs} -o {params.outdir}
        """
